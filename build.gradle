/*
   Project JaCaMo REST

   Gradle build file for JaCaMo Applications
   October 13, 2018 - 16:23:38
*/

defaultTasks 'run'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version '0.1-SNAPSHOT'
group   'org.jacamo'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven { url "http://jacamo.sourceforge.net/maven2" }
    maven { url "http://jade.tilab.com/maven/" }

    flatDir {
       dirs 'lib'
    }
}

dependencies {
	compile group: 'org.jacamo' , name: 'jacamo'   , version: '0.8-SNAPSHOT'  , changing: true , transitive: true

	compile 'javax.xml.bind:jaxb-api:+'
	compile 'com.sun.xml.bind:jaxb-core:+'
	compile 'com.sun.xml.bind:jaxb-impl:+'

	compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.27'
	compile 'org.glassfish.jersey.inject:jersey-hk2:2.27'
	compile 'org.glassfish.jersey:jersey-bom:2.27'

	compile 'org.glassfish.jersey.core:jersey-client:+'
	compile 'org.glassfish.jersey.media:jersey-media-multipart:2.27'

	// containers:
	//compile 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.27'
	compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.27'
	//compile 'org.glassfish.jersey.containers:jersey-container-jdk-http:2.27'

	compile 'org.apache.zookeeper:zookeeper:3.5.4-beta'
	compile 'org.apache.curator:curator-framework:4.0.1'
	compile 'org.apache.curator:curator-x-async:4.0.1'
	
	compile group: 'guru.nidi', name: 'graphviz-java', version: '0.7.0'
	// https://mvnrepository.com/artifact/com.google.guava/guava
	compile group: 'com.google.guava', name: 'guava', version: '11.0.2'

	//compile files ('lib/moise-0.12-SNAPSHOT.jar')	
	//compile files ('lib/jason-2.4-SNAPSHOT.jar')
}

sourceSets {
    main {
        java {
            srcDir 'src/env'
            srcDir 'src/agt'
            srcDir 'src/org'
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs the JaCaMo platform'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/main.jcm'
    classpath sourceSets.main.runtimeClasspath
}

task bob (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/bob.jcm'
    classpath sourceSets.main.runtimeClasspath
}

// Bob for docker
task bob_d (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs the JaCaMo application'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/bob-d.jcm'
    classpath sourceSets.main.runtimeClasspath
}

task marcos (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs agent marcos'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/marcos.jcm'
    classpath sourceSets.main.runtimeClasspath
}
/*
task financial (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs financial agents'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/financial.jcm'
    classpath sourceSets.main.runtimeClasspath
}*/

task test1 (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs test'
    main 'jacamo.web.ClientTest'
    classpath sourceSets.main.runtimeClasspath
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml', srcdir: 'src')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm', srcdir: '.')
    //ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm,**/*.txt', srcdir: 'doc')
}


clean {
    delete 'bin'
    delete 'build'
    delete 'log'
}


jar {
    baseName 'jacamo-rest'
}

// the following lines is used to avoid errors while generating javadoc
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'jacamo-rest'
    classifier = 'javadoc'
    from '${docsDir}/../../doc/api'
}

task sourceJar (type : Jar) {
    baseName 'jacamo-rest'
    classifier = 'sources'
    from sourceSets.main.allSource
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            artifactId 'jacamo-rest'
        }
    }
}

// transfer local maven artifact to our maven2 repository at sourceforge
task publishMavenSF(dependsOn: [ 'build', 'publishToMavenLocal']) {
  doLast {
    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/jacamo-rest'
    def tdir = 'jomifred,jacamo@web.sf.net:/home/project-web/jacamo/htdocs/maven2/org/jacamo/jacamo-rest'
    exec {
        commandLine 'scp', '-r', project.version, tdir
        workingDir wdir
    }
    exec {
        commandLine 'cp', 'maven-metadata-local.xml', 'maven-metadata.xml'
        workingDir wdir
    }
    exec {
        commandLine 'scp', 'maven-metadata.xml', tdir
        workingDir wdir
    }
  }
}
